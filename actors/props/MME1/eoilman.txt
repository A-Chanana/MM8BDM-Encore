actor MMEncOil : MMSplash
{
States
{
Spawn:
ENOL B 0
ENOL B 0 A_PlaySoundEx("misc/oil","Voice")
ENOL ABCDE 5
stop
}
}

actor ESabotall : Sabotall
{
Translation "117:117=[73,76,167]:[73,76,167]"
}

actor EOilCanister 20631
{
//$Category MM8BDME-Interactive Props
+SOLID
+SHOOTABLE
+NOTAUTOAIMED
+NODAMAGETHRUST
+FORCEYBILLBOARD
+NOBLOOD
+DONTBLAST
+QUICKTORETALIATE
+DONTDRAIN
Obituary "$OB_OILBARRELCREDIT"
scale 2.5
height 80
Radius 16
Health 1
mass 99999
painchance 256
damagefactor "Ignition", 1.0
species "OilCanister"
States
{
Spawn:
ENOP ABCD 8
loop
Pain.Ignition:
ENOP A 0
ENOP A 0 A_Die
ENOP A 1
Goto Death
Pain:
ENOP A 0 A_Stop
ENOP A 1 A_SpawnItem("PainFX",0,24)
Goto Spawn
Death:
ENOP A 0
ENOP A 0 A_ChangeFlag("SHOOTABLE",0)
ENOP A 0 A_ChangeFlag("SOLID",0)
ENOP A 1 A_Stop
ENOP A 0 A_SpawnItemEx("EOilCanisterExplosion",0,0,32,0,0,0,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
TNT1 A 700
Respawning:
TNT1 A 5 
TNT1 AAAAAAAAAA 1 A_LookEx(LOF_NOSOUNDCHECK,0,48,0,360,"Respawning")
Goto RespawnNow
RespawnNow:
TNT1 A 0 A_ClearTarget
TNT1 A 0 A_TakeInventory("CutterFlag",999)
TFOG A 3 A_PlaySoundEx("misc/SpawnIn",body)
TFOG CB 2
TNT1 A 0 A_SpawnItemEx("EOilCanister",0,0,0,0,0,0,0)
stop
}
}

actor EOilCanisterExplosion : BasicHazardExplosion
{
damagetype "OilCanister"
Obituary "$OB_OILBARRELCREDIT"
States
{
Credit:
	TNT1 E 0 A_SpawnItemEx("EOilCanisterExplodeFX")
	TNT1 A 35 ACS_NamedExecuteWithResult("core_propexplode",15,120,120)
	stop
NoCredit:
	TNT1 E 0 A_SpawnItemEx("EOilCanisterExplodeFX")
	TNT1 A 35 A_Explode(15,120,XF_HURTSOURCE,0,120)
	stop
}
}

actor EOilCanisterExplodeFX : BasicExplosionEffect
{
States
{
Spawn:
	MXP3 A 0 NoDelay A_Jump(3,"SpawnStock")
	MXP3 ABCDEFGHIJKLMN 1
	stop
SpawnStock:
	DXPL A 0 A_PlaySoundEx("misc/realblast", "Voice")
	DXPL ABCDEFGHIJKLMNOPQ 2 A_SetScale(0.7)
	stop
}
}

actor HazardCredit_EOilCanisterExplosion : BasicACSDamager
{
	DamageType "OilCanister"
	Obituary "$OB_OILBARRELCREDIT"
}

actor EncoreOilFlame
{
Translation "218:218=104:104","43:43=117:117"
+MISSILE
+FORCEYBILLBOARD
-SHOOTABLE
+NOBLOOD
+DONTBLAST
+FLOORHUGGER
+DONTREFLECT
+BRIGHT
+HITMASTER
height 64
Radius 12
Damage (0)
damagetype "Fire"
Reactiontime 35
Scale 2.0
var int user_targettid;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_JumpIf(!CallACS("core_checkservercvar", 1) || user_targettid == 0, "NoCredit")
TNT1 A 0 ACS_NamedExecuteWithResult("core_setpointer", AAPTR_TRACER, user_targettid)
SpawnLoop:
OILF AB 5 A_Countdown
loop
Death:
OILF A 0 
Stop
XDeath:
OILF A 0 ACS_NamedExecuteWithResult("core_damageowner", 10, AAPTR_MASTER, AAPTR_TRACER)
stop
NoCredit:
TNT1 A 0 A_SpawnItemEx("EncoreOilFlameNoCredit",0,0,0,0,0,0,0,1)
stop
}
}

actor EncoreOilFlameNoCredit : EncoreOilFlame
{
Damage (10)
-HITMASTER
States
{
Spawn:
	OILF AB 5 A_Countdown
	loop
Death:
	OILF A 0 
	Stop
XDeath:
	OILF A 0
	stop
}
}