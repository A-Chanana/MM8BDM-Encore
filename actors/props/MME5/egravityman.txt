actor EPressBall : PressBall 20526
{
//$Category MM8BDME-Interactive Props
Radius 32
scale 2.5
Height 95
DamageType "PressBall"
+DONTBLAST
+DONTRIP
+NOGRAVITY
+FORCEYBILLBOARD
+SHOOTABLE
+SOLID
+DONTREFLECT
-PUSHABLE
+INVULNERABLE
Health 9999999
Mass 9999999
SelfObituary "$OB_EPRESSBALL"
obituary "$OB_EPRESSBALL"
States
{
Spawn:
EPRS A 0
EPRS A 0 A_GiveInventory("EPressBallCheck",1)
SpawnLoop:
EPRS A 1 ThrustThingZ(0,20,1,1)
EPRS A 0 A_SpawnItemEx("EPressBall_Hitbox",0,0,44,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)//A_Explode(15, 38, 1, 0, 39)
EPRS A 0 A_JumpIf(z-floorz>=200,"HeightOverflow")
loop
HeightOverflow:
EPRS A 1 A_Warp(0, 0, 0, 200, 0, WARPF_TOFLOOR|WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION )
goto SpawnLoop
}
}

actor EPressBall_Hitbox
{
    SelfObituary "$OB_EPRESSBALL"
	obituary "$OB_EPRESSBALL"
    DamageType "PressBall"
    +MISSILE
    +NOTELEPORT
    -SOLID
    +NOCLIP
    +DONTBLAST
    +NOGRAVITY
    +RIPPER
    +SKYEXPLODE
    +NOINTERACTION
    +DONTSPLASH
    +THRUGHOST
    +THRUACTORS
    +DONTREFLECT
    Damage (0)
    radius 2
    height 2
    scale 2.5
    Speed 0
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1 A_Explode(15,40,0,0,41)
        stop
    Death:
        TNT1 A 1
        stop
    }
}

actor EPressBallCheck : Inventory {inventory.amount 1 inventory.maxamount 1}

actor ThrustPressUp : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
States
{
Use:
PLAY H 0 A_JumpIfInventory("EPressBallCheck",1,"Wowthrusters")
stop
Wowthrusters:
PLAY H 0 A_JumpIf(z-floorz>=200,"Maxheight")
PLAY H 0 ThrustThingZ(0,60,0,1)
stop
Maxheight:
PLAY H 0 A_Stop
stop
}
}

//It's not really interactive, it just makes more sense to keep them together.
actor EPressStand 20527 
{
//$Category MM8BDME-Interactive Props
+NOINTERACTION
+FORCEYBILLBOARD
+CLIENTSIDEONLY
-SOLID
scale 2.5
States
{
Spawn:
EPRS B -1
stop
}
}

//Gravity Up
actor EGravityUp : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
States
{
Spawn:
TNT1 A 1
loop
Pickup:
TNT1 A 0 A_JumpIfInventory("EGravityKeepUp", 1, "DontGetMeStarted")
TNT1 A 0 A_JumpIfInventory("EGravityKeepDown", 1, "DontGetMeStarted")
PLAY H 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,1)
PLAY H 0 A_PlaySoundEx("misc/gravityup","Voice")
PLAY H 0 A_GiveInventory("EGravityKeepUp")
PLAY H 0 A_SpawnItemEx("EGravityUpLoop")
DontGetMeStarted:
TNT1 A 0
stop
}
}

actor EGravityKeepUp : PowerUp
{
	powerup.duration -5
}

actor EGravityKeepDown : PowerUp
{
	powerup.duration -6
}

actor EGravityUpLoop
{
Height 0
Radius 0
+NOINTERACTION
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION)
TNT1 A 1 A_SpawnItemEx("EGravityUpParticles",random(-70,70),random(-70,70),floorz-z+5,0,0,26,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_RadiusGive("ThrustPressUp",250,RGF_OBJECTS|RGF_NOSIGHT,1)
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 1 A_JumpIfInTargetInventory("EGravityKeepUp",1,"Spawn")
Death:
TNT1 A 1 A_GiveToTarget("EGravityDown")
stop
}
}

actor EGravityUpParticles
{
    PROJECTILE
	+NOINTERACTION
	+CLIENTSIDEONLY
	translation "198:198=41:41"
    height 32
    radius 32
    speed 50
	scale 2.5
	Reactiontime 105
    States
    {
    Spawn:
		INTQ C 0
        INTQ C 1 A_JumpIfInTargetInventory("IsDead", 1, "Death")
		INTQ C 0 A_JumpIfInTargetInventory("EGravityKeepDown", 1, "Transform")
		INTQ C 0 A_Countdown
		loop
	Transform:
		TNT1 A 1 A_SpawnItemEx("EGravityDownParticles",0,0,0,0,0,-26,0,SXF_CLIENTSIDE)
		stop
	Death:
		INTQ C 0
		stop
    }
}

//Gravity Down
actor EGravityDown : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
States
{
Spawn:
TNT1 A 1
loop
Pickup:
TNT1 A 0
PLAY H 0 ACS_NamedExecuteAlways("core_flyplayer",0,0,0)
PLAY H 0 A_PlaySoundEx("misc/gravitydown","Voice")
PLAY H 0 A_GiveInventory("EGravityKeepDown")
PLAY H 0 A_SpawnItemEx("EGravityDownLoop")
stop
}
}

actor EGravityDownLoop
{
Height 0
Radius 0
+NOINTERACTION
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0 A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION)
TNT1 A 1 A_SpawnItemEx("EGravityDownParticles",random(-70,70),random(-70,70),ceilingz-z-5,0,0,-26,0,SXF_TRANSFERPOINTERS)
TNT1 A 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
TNT1 A 1 A_JumpIfInTargetInventory("EGravityKeepDown",1,"Spawn")
Death:
TNT1 A 1 //A_GiveToTarget("EGravityDown")
stop
}
}

actor EGravityDownParticles
{
    PROJECTILE
	+NOINTERACTION
	+CLIENTSIDEONLY
    height 32
    radius 32
    speed 50
	scale 2.5
	Reactiontime 105
    States
    {
    Spawn:
		INTQ C 1
        INTQ C 1 A_JumpIfInTargetInventory("IsDead", 1, "Death")
		INTQ C 0 A_JumpIfInTargetInventory("EGravityKeepUp", 1, "Transform")
		INTQ C 0 A_Countdown
		loop
	Transform:
		TNT1 A 1 A_SpawnItemEx("EGravityUpParticles",0,0,0,0,0,26,0,SXF_CLIENTSIDE)
		stop
	Death:
		INTQ C 0
		stop
    }
}