actor AirconManNoWaveSpawner_P : Powerup
{
    powerup.duration 350
}

actor AirconManGiantWave
{
+DONTSPLASH
+MISSILE
+BRIGHT
+THRUACTORS
+NOGRAVITY
+STEPMISSILE
maxstepheight 24
scale 2.5
Height 60
Radius 60
States
{
Spawn:
WWAV AABBCCDDCCBBAA 1 A_RadiusGive("AirconManGiantWave_Thrust_P",128,RGF_PLAYERS|RGF_MONSTERS,1)
Stop
}
}

Const int AIRCONWAVEHORITHRUST = 90;
Const int AIRCONWAVEVERTTHRUST = 12;

actor AirconManGiantWave_Thrust_P : CustomInventory
{
    States
    {
    Pickup:
		TNT1 A 0 A_JumpIf(CallACS("Encore_AirconGiantWaveButtonCheck") == 1, "Pickup2")
        TNT1 A 0 A_ChangeVelocity(AIRCONWAVEHORITHRUST,0,0,CVF_REPLACE)
        stop
	Pickup2:
		TNT1 A 0 A_ChangeVelocity(AIRCONWAVEHORITHRUST,0,AIRCONWAVEVERTTHRUST,CVF_REPLACE)
		stop
    }
}

//Thanks Pegg
actor AirconManMistSpawner 21749
{
//$Category MM8BDME-Props
+CLIENTSIDEONLY
+NOINTERACTION
+NOGRAVITY
-SOLID
states
{
Spawn:
TNT1 AA 0 A_SpawnItemEX("AirconManDroplet",0,random(-4,4),0,random(5,10),random(-4,4),random(5,10),0,0)
TNT1 A 10
TNT1 AA 0 A_SpawnItemEX("AirconManDroplet",0,random(-4,4),0,random(5,10),random(-4,4),random(5,10),0,0)
TNT1 A 10
TrueLoop:
TNT1 A 0 A_PlaySound("misc/fullenglish",7,0.4,0,ATTN_STATIC)
TNT1 A 10 A_SpawnItemEX("AirconManMist",-10,random(-4,4),0,2,random(-1,1),random(0,1),0,0)
TNT1 AA 0 A_SpawnItemEX("AirconManDroplet",0,random(-4,4),0,random(5,10),random(-4,4),random(5,10),0,0)
TNT1 A 10 A_SpawnItemEX("AirconManMist",-10,random(-4,4),0,2,random(-1,1),random(0,1),0,0)
TNT1 A 0 A_PlaySound("misc/fullenglish",7,0.4,0,ATTN_STATIC)
TNT1 AA 0 A_SpawnItemEX("AirconManDroplet",0,random(-4,4),0,random(5,10),random(-4,4),random(5,10),0,0)
TNT1 A 10 A_SpawnItemEX("AirconManMist",-10,random(-4,4),0,2,random(-1,1),random(0,1),0,0)
TNT1 AA 0 A_SpawnItemEX("AirconManDroplet",0,random(-4,4),0,random(5,10),random(-4,4),random(5,10),0,0)
TNT1 A 10 A_SpawnItemEX("AirconManMist",-10,random(-4,4),0,2,random(-1,1),random(0,1),0,0)
TNT1 AA 0 A_SpawnItemEX("AirconManDroplet",0,random(-4,4),0,random(5,10),random(-4,4),random(5,10),0,0)
loop
}
}

actor AirconManMist
{
translation "198:198=196:196", "192:192=196:196"
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
+NOINTERACTION
+NOGRAVITY
-SOLID
scale 2.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetScale(0.5,1.0)
TNT1 A 0 A_Jump(128,"2")
W_RC PPPPPPPPPP 4 A_Fadeout(0.1)
stop
2:
TNT1 A 0
W_RC OOOOOOOOOO 4 A_Fadeout(0.1)
stop
}
}

actor AirconManDroplet
{
+CLIENTSIDEONLY
+FORCEXYBILLBOARD 
+DONTSPLASH
-NOGRAVITY
-SOLID
scale 1.0
States
{
Spawn:
TNT1 A 0
PIXL A 15
stop
}
}