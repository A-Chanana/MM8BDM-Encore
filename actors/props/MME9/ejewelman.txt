//-----------
//Power Jewel
//-----------

actor EncorePowerJewel 21724
{
//$Category MM8BDME-Interactive Props
//$Arg0 Size
	+NOGRAVITY
	+FORCEYBILLBOARD
	+QUICKTORETALIATE
	+DONTBLAST
	+LOOKALLAROUND
	Height 48
	Radius 9
	Scale 2.0
	States
	{
	Spawn:
	EPJW A 4 A_RadiusGive("JewelPowerups_P",Args[0],RGF_PLAYERS,1)
	EPJW AAAA 0 A_SpawnItemEx("EncoreJewelShotFX1",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW AAAA 0 A_SpawnItemEx("EncoreJewelShotFX2",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW AAAA 0 A_SpawnItemEx("EncoreJewelShotFX3",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW B 4 A_RadiusGive("JewelPowerups_P",Args[0],RGF_PLAYERS,1)
	EPJW BBBB 0 A_SpawnItemEx("EncoreJewelShotFX1",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW BBBB 0 A_SpawnItemEx("EncoreJewelShotFX2",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW BBBB 0 A_SpawnItemEx("EncoreJewelShotFX3",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW C 4 A_RadiusGive("JewelPowerups_P",Args[0],RGF_PLAYERS,1)
	EPJW CCCC 0 A_SpawnItemEx("EncoreJewelShotFX1",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW CCCC 0 A_SpawnItemEx("EncoreJewelShotFX2",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW CCCC 0 A_SpawnItemEx("EncoreJewelShotFX3",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW B 4 A_RadiusGive("JewelPowerups_P",Args[0],RGF_PLAYERS,1)
	EPJW BBBB 0 A_SpawnItemEx("EncoreJewelShotFX1",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW BBBB 0 A_SpawnItemEx("EncoreJewelShotFX2",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	EPJW BBBB 0 A_SpawnItemEx("EncoreJewelShotFX3",random(-Args[0],Args[0]),random(-Args[0],Args[0]),Random(-Args[0],Args[0]),0,0,0,0,SXF_ABSOLUTEMOMENTUM)
	loop
	}
}

// Using radiusgive is way more intensive than this
actor JewelPowerups_P : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("JewelSpeedPowerup")
TNT1 A 0 A_GiveInventory("JewelJumpPowerup")
TNT1 A 0 A_GiveInventory("EncoreJewelPlayerIndicator")
stop
}
}

actor JewelSpeedPowerup : PowerSpeed
{
Powerup.duration 5
Powerup.Strength 1.2
Speed 1.2
Inventory.amount 1
Inventory.maxamount 1
}

actor JewelJumpPowerup : PowerHighJump
{
Powerup.duration 5
Powerup.Strength 1.3
Inventory.amount 1
Inventory.maxamount 1
}

actor EncoreJewelShotFX1 : BasicGraphicEffect
{
renderstyle translucent
alpha 0.8
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
+BRIGHT
translation "192:192=171:171"
States
{
Spawn:
JEWS WVUVW 3
stop
}
}
actor EncoreJewelShotFX2 : EncoreJewelShotFX1 {translation "192:192=64:64"}
actor EncoreJewelShotFX3 : EncoreJewelShotFX1 {translation "192:192=101:101"}

Actor EncoreJewelPlayerIndicator : CustomInventory
{
States
{
Pickup:
TNT1 AA 0 A_SpawnItemEx("EncoreJewelShotFX1",random(-64,64),random(-64,64),Random(-64,64),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
TNT1 AA 0 A_SpawnItemEx("EncoreJewelShotFX2",random(-64,64),random(-64,64),Random(-64,64),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
TNT1 AA 0 A_SpawnItemEx("EncoreJewelShotFX3",random(-64,64),random(-64,64),Random(-64,64),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
stop
}
}

//-----------
//Other Props
//-----------

actor EncoreJewelCrystal : JewelCrystal 21719
{
//$Category MM8BDME-Props
States
{
Spawn:
EJGM A -1
loop
}
}

actor EncoreCaricarry : Caricarry 21720
{
//$Category MM8BDME-Props
States
{
Spawn:
ECRI A 0
ECRI A 0 ACS_NamedExecuteAlways("core_stickyLifts",0,STICKYMODE_GUTSLIFT)
ECRI B 0 A_JumpIf(momx==0 && momy==0,"StyleChange") //If train stops,go to more laggy,but more reliable alternative
ECRI A 3 A_ChangeVelocity(7,0,momz,CVF_RELATIVE)
ECRI A 0 A_Stop
ECRI B 0
ECRI A 3 A_ChangeVelocity(7,0,momz,CVF_RELATIVE)
ECRI A 0 A_Stop
ECRI B 0
ECRI B 3 A_ChangeVelocity(7,0,momz,CVF_RELATIVE)
ECRI A 0 A_Stop
ECRI B 0
ECRI B 3 A_ChangeVelocity(7,0,momz,CVF_RELATIVE)
ECRI A 0 A_Stop
ECRI B 0
ECRI B 0
Goto Spawn+2
StyleChange:
ECRI A 3 A_Recoil(-6)
ECRI A 0 A_Stop
ECRI B 0
ECRI A 3 A_Recoil(-6)
ECRI A 0 A_Stop
ECRI B 0
ECRI B 3 A_Recoil(-6)
ECRI A 0 A_Stop
ECRI B 0
ECRI B 3 A_Recoil(-6)
ECRI A 0 A_Stop
ECRI B 0
ECRI B 0
Goto Spawn+2
}
}

actor EncoreJewelChain : JewelChain 21721
{
//$Category MM8BDME-Props
States
{
Spawn:
EJCH A 0
TNT1 A 0 A_JumpIf(ceilingz - z < 16,"Death")
TNT1 A 1 A_SpawnItemEx("EncoreJewelChainFx",0,0,16)
stop
Death:
TNT1 A 0
stop
}
}


actor EncoreJewelChainFx : JewelChainFx
{
States
{
Spawn:
EJHC A 0
EJCH A 0 A_JumpIf(ceilingz - z < 16,"Death")
EJCH A 0 A_SpawnItemEx("EncoreJewelChainFx",0,0,52)
EJCH A -1
stop
Death:
TNT1 A 0
stop
}
}

actor EncoreJewelFloorSpike : Spiney 21722
{
states
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_SpawnItemEx("SpikeDamager")
goto Graphic
Graphic:
EHZJ A 6
EHZJ B 6
EHZJ C 6
EHZJ B 6
loop
}
}

actor EncoreStoneHead : StoneHead 21723
{
//$Category MM8BDME-Props
States
{
Spawn:
	EHED A -1
	wait
Face:
	EHED B 4
	EHED C 53
	EHED B 4
	goto Spawn
}
}


actor EncoreUltraPowerJewels 21725
{
//$Category MM8BDME-Props
//$Arg0 Color/Style
+SOLID
+NOGRAVITY
+FORCEYBILLBOARD
Height 4
Radius 4
Scale 2.0
States
{
Spawn:
ESJW A 0
ESJW A 0 A_JumpIf(args[0]==1,"Color2")
ESJW A 0 A_JumpIf(args[0]==2,"Color3")
ESJW A 0 A_JumpIf(args[0]==3,"Color4")
ESJW A 0 A_JumpIf(args[0]==4,"Color5")
ESJW A 0 A_JumpIf(args[0]==5,"Color6")
ESJW A 0 A_JumpIf(args[0]==6,"Color7")
ESJW A 0 A_JumpIf(args[0]==7,"StyleAlt")
Color1:
ESJW A 6
ESJW B 6
ESJW C 6
ESJW B 6
loop
Color2:
ESJW A 0 A_SpawnItemEx("EncoreUltraPowerJewelBlue")
stop
Color3:
ESJW A 0 A_SpawnItemEx("EncoreUltraPowerJewelYellow")
stop
Color4:
ESJW A 0 A_SpawnItemEx("EncoreUltraPowerJewelGreen")
stop
Color5:
ESJW A 0 A_SpawnItemEx("EncoreUltraPowerJewelPurple")
stop
Color6:
ESJW A 0 A_SpawnItemEx("EncoreUltraPowerJewelWhite")
stop
Color7:
ESJW A 0 A_SpawnItemEx("EncoreUltraPowerJewelLightBlue")
stop
StyleAlt:
ESJW A 0 A_SpawnItemEx("EncoreSupremePowerJewel")
stop
}
}

actor EncoreUltraPowerJewelBlue : EncoreUltraPowerJewels
{
translation "175:175=76:76","171:171=69:69","173:173=70:70","170:170=67:67","182:182=78:78"
}

actor EncoreUltraPowerJewelYellow : EncoreUltraPowerJewels
{
translation "175:175=219:219","171:171=228:228","173:173=216:216","170:170=160:160","182:182=222:222"
}

actor EncoreUltraPowerJewelGreen : EncoreUltraPowerJewels
{
translation "175:175=118:118","171:171=109:109","173:173=116:116","170:170=103:103","182:182=119:119"
}

actor EncoreUltraPowerJewelPurple : EncoreUltraPowerJewels
{
translation "175:175=251:251","171:171=237:237","173:173=238:238","170:170=56:56","182:182=254:254"
}

actor EncoreUltraPowerJewelWhite : EncoreUltraPowerJewels
{
translation "175:175=92:92","171:171=85:85","173:173=88:88","170:170=82:82","182:182=3:3"
}

actor EncoreUltraPowerJewelLightBlue : EncoreUltraPowerJewels
{
translation "175:175=205:205","171:171=203:203","173:173=204:204","170:170=201:201","182:182=207:207"
}

actor EncoreSupremePowerJewel
{
+SOLID
+NOGRAVITY
+FORCEYBILLBOARD
Height 42
Radius 128
Scale 2.0
States
{
Spawn:
EMJW A 6
EMJW B 6
EMJW C 6
EMJW B 6
loop
}
}

actor JewelParticleGenerator 20827
{
//$Category MM8BDME-Props
//$Arg2 Starting delay (in ticks)
//$NotAngled
height 2
radius 2
+NOINTERACTION
+NOGRAVITY
renderstyle none
+CLIENTSIDEONLY
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetTics(args[2])
Spawn2:
TNT1 A 1 A_Jump(256,"A","B","C")
wait
A:
TNT1 A 35 A_SpawnItemEx("EncoreJewelShotFX1Big",0,0,0,0,0,5) 
goto Spawn2
B:
TNT1 A 35 A_SpawnItemEx("EncoreJewelShotFX2Big",0,0,0,0,0,5) 
goto Spawn2
C:
TNT1 A 35 A_SpawnItemEx("EncoreJewelShotFX3Big",0,0,0,0,0,5) 
goto Spawn2
}
}

actor EncoreJewelShotFX1Big : EncoreJewelShotFX1
{
scale 3.5
States
{
Spawn:
JEWS WVUVW 4
stop
}
}
actor EncoreJewelShotFX2Big : EncoreJewelShotFX1Big {translation "192:192=64:64"}
actor EncoreJewelShotFX3Big : EncoreJewelShotFX1Big {translation "192:192=101:101"}