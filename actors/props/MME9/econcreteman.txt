actor EncoreConcreteSpike 21713
{
+DONTBLAST
+NOGRAVITY
+THRUGHOST
Height 16
radius 16
scale 2.5
states
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_SpawnItemEx("SpikeDamager") // From Spiney
goto Graphic
Graphic:
ECNS A -1
stop
}
}

actor EncorePaozo 21706
{
//$Arg0 Color
//$Arg0Enum { 0 = "Red"; 1 = "Green"; }
//$Arg0Type 11
+SOLID
height 128
radius 64
scale 2.5
States
{
Spawn:
EPAZ A 0
EPAZ A 0 A_JumpIf(args[0]>0, "Flavor2")
Flavor1:
EPAZ A 1
loop
Flavor2:
EPAZ A 0 A_SpawnItemEx("EncoreGreenPaozo")
stop
}
}

actor EncoreGreenPaozo : EncorePaozo
{
translation "183:183=119:119"
}

actor EncorePaozoBall 21707
{
//$Arg0 Color
//$Arg0Enum { 0 = "Red"; 1 = "Green"; }
//$Arg0Type 11
+SOLID
height 64
radius 32
scale 2.5
States
{
Spawn:
EPAZ B 0
EPAZ B 0 A_JumpIf(args[0]>0, "Flavor2")
Flavor1:
EPAZ B 1
loop
Flavor2:
EPAZ A 0 A_SpawnItemEx("EncoreGreenPaozoBall")
stop
}
}

actor EncoreGreenPaozoBall : EncorePaozoBall
{
translation "183:183=119:119"
}

actor EncoreHoohoo
{
PROJECTILE
+DONTBLAST
+RIPPER
+BLOODLESSIMPACT
-SOLID
+NOGRAVITY
+DONTREFLECT
+SHOOTABLE
+NOTAUTOAIMED
+NOBLOOD
-NOBLOCKMAP
+DONTDRAIN
+NODAMAGE
Painchance 256
Health 1
Damage (0)
radius 16
height 16
scale 2.5
speed 0
States
{
Spawn:
ECNB AA 0 A_ChangeVelocity(10, 0, 0, CVF_REPLACE|CVF_RELATIVE)
ECNB A 4 A_Weave(0, 6, 0, 1.0)
ECNB A 4 A_Weave(0, 6, 0, 1.0)
ECNB B 4 A_Weave(0, 6, 0, 1.0)
ECNB B 4 A_Weave(0, 6, 0, 1.0)
loop
Pain:
ECNB C 0 A_ChangeFlag(NOPAIN, true)
ECNB C 0 A_ClearTarget
ECNB C 0 A_SpawnItemEx("EncoreHoohooRock", 0, 0, -10, momx/2)
ECNB C 8 ThrustThingZ(0, 12, 0, 0)
ECNB D 4 A_ChangeVelocity(10, 0, momz/2, CVF_REPLACE|CVF_RELATIVE)
ECNB D 4 A_ChangeVelocity(10, 0, 0, CVF_REPLACE|CVF_RELATIVE)
Goto Spawn2
Spawn2:
ECNB EEDD 4 A_Weave(0, 6, 0, 1.5)
loop
}
}

actor EncoreHoohooRock
{
PROJECTILE
-NOGRAVITY
+DONTREFLECT
+RIPPER
Damage 0
Speed 0
radius 5
height 5
Scale 2.5
States
{
Spawn:
ECNB F 1
loop
Death:
ECNB FFFF 0 A_SpawnItemEx("EncoreHoohooShrapnel", 0, 0, 0, random(1, 5), 0, random(5, 10), random(0, 359))
stop
}
}

actor EncoreHoohooShrapnel
{
PROJECTILE
-NOGRAVITY
+RIPPER
+DONTREFLECT
Damage 0
Speed 0
radius 5
height 5
Scale 2.5
States
{
Spawn:
ECNB I 0
ECNB I 0 A_Jump(64, "Spawn1")
ECNB I 0 A_Jump(96, "Spawn2")
ECNB I 1
wait
Spawn1:
ECNB G 1
wait
Spawn2:
ECNB H 1
wait
}
}